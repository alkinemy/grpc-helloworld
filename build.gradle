plugins {
    id "java-library"
    id "idea"

    id "com.google.protobuf" version "0.8.10"
}

ext {
    lombokVersion = '1.18.8'
    slf4jVersion = '1.7.26'
    logbackVersion = '1.2.3'

    protobufVersion = '1.22.1'

    grpcStubDir = "${buildDir}/generated/source/proto/main/grpc/"
    grpcStructureDir = "${buildDir}/generated/source/proto/main/java/"
}

group = 'al.grpc.helloworld'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11


configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'org.slf4j', module: 'slf4j-jcl'
    all*.exclude group: 'org.slf4j', module: 'slf4j-jdk14'

    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.8.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${protobufVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}


sourceSets {
    main {
        java {
            srcDirs += file("${grpcStubDir}")
            srcDirs += file("${grpcStructureDir}")
        }
    }
}

idea {
    module {
        generatedSourceDirs += file("${grpcStubDir}")
        generatedSourceDirs += file("${grpcStructureDir}")
    }
}

dependencies {
    compile("org.slf4j:slf4j-api:${slf4jVersion}")
    compile("org.slf4j:jcl-over-slf4j:${slf4jVersion}")
    compile("org.slf4j:log4j-over-slf4j:${slf4jVersion}")
    compile("org.slf4j:jul-to-slf4j:${slf4jVersion}")
    compile("ch.qos.logback:logback-core:${logbackVersion}")
    compile("ch.qos.logback:logback-classic:${logbackVersion}")

    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    testCompileOnly("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")


    compile group: 'io.grpc', name: 'grpc-netty', version: "${protobufVersion}"
    compile group: 'io.grpc', name: 'grpc-protobuf', version: "${protobufVersion}"
    compile group: 'io.grpc', name: 'grpc-stub', version: "${protobufVersion}"

    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.8.0'

    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    testImplementation("org.junit.jupiter:junit-jupiter:5.5.0")
}

test {
    useJUnitPlatform()
}
